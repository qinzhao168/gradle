/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Wrapper main entry point requires Java 5
sourceCompatibility = javaVersion.java9Compatible ? 1.6 : 1.5

dependencies {
    compile project(":cli")

    testCompile libraries.groovy
    testCompile libraries.ant

    integTestRuntime rootProject.configurations.testRuntime.allDependencies
}

ext {
    wrapperBuildReceipt = "build-receipt.properties"
}

task wrapperBuildReceiptResource() {
    inputs.property 'version', { version }
    outputs.file { "${generatedTestResourcesDir}/${wrapperBuildReceipt}" }
    outputs.cacheIf { true }
    doLast {
        generatedTestResourcesDir.mkdirs()
        new File(generatedTestResourcesDir, wrapperBuildReceipt).text = "versionNumber=${version}"
    }
}
sourceSets.test.output.dir generatedTestResourcesDir, builtBy: wrapperBuildReceiptResource

task executableJar(type: Jar) {
    archiveName = 'gradle-wrapper.jar'
    from sourceSets.main.output
    from configurations.runtime.allDependencies.withType(ProjectDependency).collect { it.dependencyProject.sourceSets.main.output }
}

integTestTasks.all {
    inputs.files { project(":distributions").binZip }
    requiresBinDist = true
}

useTestFixtures()
useClassycle()
